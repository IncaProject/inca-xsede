#!/usr/bin/env perl

use strict;
use warnings;
use Inca::Reporter::SimpleUnit;
use DBI;

my $reporter = new Inca::Reporter::SimpleUnit(
	name => 'data.db.gateways',
	version => 1,
	description => 'Checks the central database for entries in the job_attribute_stage table without a job_id.',
);
$reporter->addDependency('DBI');
$reporter->addDependency('DBD::Pg');
$reporter->addArg('days', 'Number of days to exclude from search', '2');
$reporter->addArg('startdate', 'Date after which to search', '2015-04-01 0:0:0');
$reporter->addArg('resources', 'Comma separated resources with entries in the job_attribute_stage table', 'gordon.sdsc.teragrid,stampede.tacc.xsede');
$reporter->processArgv(@ARGV);
my $days = $reporter->argValue('days');
my $start_date = $reporter->argValue('startdate');
my $resources = $reporter->argValue('resources');

my $dbh = DBI->connect( "DBI:Pg:dbname=teragrid;host=tgcdb.xsede.org;port=5432", "gw_att_pub", undef, { RaiseError=>1, PrintError=>1 } );
if ( !$dbh ) 
{
	$reporter->failPrintAndExit( "Unable to connect to TGCDB" );
}

# Get a list of unmatched job_attribute_stage entries
my $query = "SELECT * FROM acct.job_attribute_stage AS s
	JOIN acct.jobs AS j ON j.resource_id=s.resource_id AND j.local_jobid=s.local_jobid
	WHERE s.job_id IS NULL 
	AND s.submit_time < CAST( NOW() - INTERVAL '$days days' AS timestamp ) 
	AND s.submit_time > CAST( '$start_date' AS timestamp )
	ORDER BY s.job_attribute_stage_id";

$reporter->log( 'debug', $query );

my @errs;
my $sth = $dbh->prepare( qq { $query } );
$sth->execute();
if ( $sth->rows != 0 ) 
{
	push( @errs, "Found entries in the job_attribute_stage table without a job_id:" );
	while ( my @datarow = $sth->fetchrow() ) 
	{
		no warnings 'uninitialized';
		push( @errs, join( "\t", @datarow ) );
	}
}

# Get resources that have recent job_attribute_stage entries
$query = "SELECT DISTINCT resource_name FROM acct.job_attribute_stage 
	WHERE submit_time > CAST( NOW() - INTERVAL '$days days' AS timestamp )
	ORDER BY resource_name"; 

$reporter->log( 'debug', $query );
$sth = $dbh->prepare( qq { $query } );
$sth->execute();
if ( $sth->rows == 0 ) 
{
	push( @errs, 'No resources with recent job_attribute_stage entries' );
}
else
{
  my @db_resources;
	while ( my @datarow = $sth->fetchrow() ) 
	{
		push( @db_resources, @datarow );
	}
	my @resources = split( ',', $resources );
	foreach my $resource ( @resources )
	{
		if ( ! grep { $_ eq $resource } @db_resources )
		{
			push( @errs, 'No recent job_attribute_stage entries for ' . $resource );
		}
	} 
}

$sth->finish();
$dbh->disconnect();

if ( scalar( @errs ) != 0 )
{
	$reporter->failPrintAndExit( join( "\n", @errs ) );
}


$reporter->unitSuccess();
$reporter->print();
