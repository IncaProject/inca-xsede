#!/usr/bin/env perl

use strict;
use warnings;
use Inca::Reporter::SimpleUnit;
use DBI;

my $reporter = new Inca::Reporter::SimpleUnit(
	name => 'data.db.gateways',
	version => 3,
	description => 'Checks the central database for entries in the job_attribute_stage table without a job_id.',
);
$reporter->addDependency('DBI');
$reporter->addDependency('DBD::Pg');
$reporter->addArg('waitdays', 'Number of days to wait for jobs to complete', '2', '\d+');
$reporter->addArg('checkdays', 'Number of days to check for missing job_id entries (after job complete wait days)', '7', '\d+');
$reporter->addArg('resources', 'Comma separated resources that should have entries in the job_attribute_stage table', 'comet.sdsc.xsede,gordon.sdsc.teragrid,maverick.tacc.xsede,stampede.tacc.xsede');
$reporter->processArgv(@ARGV);
my $wait_days = $reporter->argValue('waitdays');
my $check_days = $reporter->argValue('checkdays');
my $search_days = $wait_days + $check_days;
my $resources_list = $reporter->argValue('resources');
my @resources = split( ',', $resources_list );
my @quoted_resources = map{"'$_'"} @resources;
my $sql_match_resources = join(",", @quoted_resources);

my $dbh = DBI->connect( "DBI:Pg:dbname=teragrid;host=tgcdb.xsede.org;port=5432", "gw_att_pub", undef, { RaiseError=>1, PrintError=>1 } );
if ( !$dbh ) 
{
	$reporter->failPrintAndExit( "Unable to connect to TGCDB" );
}

# Get a list of unmatched job_attribute_stage entries
my $query = "SELECT s.value, 
		s.submit_time AS submit_time_stage, 
		j.submit_time AS submit_time_jobs, 
		s.resource_name, 
		j.local_jobid 
	FROM acct.job_attribute_stage AS s
	JOIN acct.jobs AS j ON j.resource_id=s.resource_id AND j.local_jobid=s.local_jobid
	WHERE s.job_id IS NULL 
	AND s.resource_name in ($sql_match_resources) 
	AND s.submit_time < CAST( NOW() - INTERVAL '$wait_days days' AS timestamp ) 
	AND s.submit_time > CAST( NOW() - INTERVAL '$search_days days' AS timestamp ) 
	ORDER BY s.job_attribute_stage_id";

$reporter->log( 'debug', $query );

my @errs;
my $sth = $dbh->prepare( qq { $query } );
$sth->execute();
if ( $sth->rows != 0 ) 
{
	push( @errs, "Found entries in the job_attribute_stage table without a job_id between $wait_days and $search_days days ago" );
  my @missing;
	push( @missing, join( "\t", @{$sth->{NAME}} ) );
	while ( my @datarow = $sth->fetchrow() ) 
	{
		no warnings 'uninitialized';
		push( @missing, join( "\t", @datarow ) );
	}
  $reporter->log( 'debug', "The following " . $sth->rows . " entries in the job_attribute_stage table are missing a job_id:\n " . join( "\n", @missing ) );
}

# Get resources that have recent job_attribute_stage entries
$query = "SELECT DISTINCT resource_name FROM acct.job_attribute_stage 
	WHERE submit_time > CAST( NOW() - INTERVAL '$wait_days days' AS timestamp )
	ORDER BY resource_name"; 

$reporter->log( 'debug', $query );
$sth = $dbh->prepare( qq { $query } );
$sth->execute();
if ( $sth->rows == 0 ) 
{
	push( @errs, 'No resources with recent job_attribute_stage entries' );
}
else
{
  my @db_resources;
	while ( my @datarow = $sth->fetchrow() ) 
	{
		push( @db_resources, @datarow );
	}
	foreach my $resource ( @resources )
	{
		if ( ! grep { $_ eq $resource } @db_resources )
		{
			push( @errs, 'No recent job_attribute_stage entries for ' . $resource );
		}
	} 
}

$sth->finish();
$dbh->disconnect();

if ( scalar( @errs ) != 0 )
{
	$reporter->failPrintAndExit( join( "\n", @errs ) );
}


$reporter->unitSuccess();
$reporter->print();
